DEFINE DATA LOCAL /* SAMPLES
1 SK-NUM  (A21/1:15)
1 SK-OPER (A1/1:15)
1 CV-NUM  (C/1:15)
1 CV-OPER (C/1:15)
1 WK-TOTAL (N17,4)
1 IND      (N2)
1 IND2     (N2)
1 IND3     (N2)
1 WK-NUM  (N15,4/1:15)
END-DEFINE
ON ERROR
  IF  *ERROR-NR = 1301 OR = 1305
    SET CONTROL 'WFL40C03B17/01'
    INPUT    '*** RESULTADO DEMASIADO GRANDE ***'
    FETCH 'CALC'
  END-IF
END-ERROR
SET KEY PF2
SET KEY PF3
SET KEY PF4
SET KEY PF5
REPEAT
MOVE (AD=NP) TO CV-OPER(1)
RESET SK-OPER(1)
SET CONTROL 'WFL40C24B1/1'
INPUT MAP 'MCALC'
IF *PF-KEY = 'PF3'
   SET CONTROL 'WBM'
   ESCAPE BOTTOM
END-IF
IF *PF-KEY = 'PF4'
   PERFORM ADIOS
   SET CONTROL 'WBM'
   ESCAPE BOTTOM
END-IF
IF *PF-KEY = 'PF5'
   PERFORM CIAO
   SET CONTROL 'WBM'
   ESCAPE BOTTOM
END-IF
IF *PF-KEY = 'PF2'
   RESET SK-NUM(*)  SK-OPER(*)
   MOVE (AD=D) TO CV-NUM(*) CV-OPER(*)
   ESCAPE TOP
END-IF
PERFORM DEPURAR
PERFORM OPERACION
PERFORM ORGANIZAR
END-REPEAT
*
DEFINE OPERACION
EXAMINE SK-OPER(*) FOR '=' GIVING INDEX IN IND2
IF IND2 NE 0
   COMPUTE WK-TOTAL = VAL(SK-NUM(IND2))
ELSE
   COMPUTE WK-TOTAL = VAL(SK-NUM(1))
END-IF
ADD 1 TO IND2
FOR IND = IND2 TO 15
   DECIDE ON FIRST VALUE OF SK-OPER(IND)
     VALUE '+'
         PERFORM SUMA
     VALUE '-'
         PERFORM RESTA
     VALUE '*'
         PERFORM MULTIPLICA
     VALUE '/'
         PERFORM DIVIDE
     VALUE ' '
         IF SK-NUM(IND) NE ' ' AND IND NE 1
            REINPUT FULL 'TECLEE OPERACION' MARK *SK-OPER(IND)
         END-IF
     NONE
         REINPUT FULL 'OPERACION ERRONEA' MARK *SK-OPER(IND)
   END-DECIDE
END-FOR
END-SUBROUTINE
*
DEFINE DEPURAR
IF SK-OPER(*) NE ' '
   IGNORE
ELSE
   REINPUT FULL 'TECLEE OPERACIONES O PULSE PF3' MARK *SK-OPER(2)
END-IF
IF SK-NUM(1) = ' '
   REINPUT FULL 'TECLEE NUMERO' MARK *SK-NUM(1)
END-IF
IF SK-NUM(1) IS (N15,4)
   IGNORE
ELSE
 REINPUT FULL 'DEBE SER (N15,4) MAXIMO' MARK *SK-NUM(1)
END-IF
**
FOR IND = 3 TO 15
    IF SK-OPER (IND - 1) = ' ' AND SK-NUM (IND - 1) = ' '
     AND (SK-OPER(IND) NE ' ' OR SK-NUM(IND) NE ' ')
       REINPUT FULL 'NO TE SALTES LINEAS' MARK *SK-OPER(IND)
    END-IF
END-FOR
**
END-SUBROUTINE
**
DEFINE ORGANIZAR
MOVE (AD=D) TO CV-OPER(*) CV-NUM(*)
EXAMINE SK-OPER(*) FOR '=' GIVING INDEX IN IND
IF IND > 0
** MOVE SK-NUM(IND) TO SK-NUM(1)
   MOVE 1 TO IND3
   FOR IND2 = 1 TO 15
       IF IND2 < IND
          RESET SK-OPER(IND2) RESET SK-NUM(IND2)
       ELSE
          MOVE SK-NUM(IND2) TO SK-NUM(IND3)
          MOVE SK-OPER(IND2) TO SK-OPER(IND3)
          RESET SK-OPER(IND2) SK-NUM(IND2)
          ADD 1 TO IND3
       END-IF
   END-FOR
END-IF
FOR IND2 = 1 TO 15
    IF SK-NUM (IND2) NE ' '
       MOVE (AD=P) TO CV-NUM(IND2) CV-OPER(IND2)
    ELSE
       IF IND2 = 2
          MOVE (AD=D) TO CV-NUM(1)
          ESCAPE ROUTINE
       END-IF
       MOVE EDITED WK-TOTAL (EM=-ZZZZZZZZZZZZZZ9,9999) TO SK-NUM(IND2)
       MOVE '=' TO SK-OPER(IND2)
       MOVE (AD=PI) TO CV-NUM(IND2) CV-OPER(IND2)
       ESCAPE ROUTINE
    END-IF
END-FOR
IF IND2 > 15
   RESET SK-OPER(*) SK-NUM(*)
   MOVE (AD=D) TO CV-OPER(*) CV-NUM(*)
   MOVE EDITED WK-TOTAL (EM=-ZZZZZZZZZZZZZZ9,9999) TO SK-NUM(1)
END-IF
END-SUBROUTINE
*
DEFINE SUMA
IF SK-NUM(IND) IS (N15,4)
   IGNORE
ELSE
  REINPUT FULL 'TECLEE NUMERO (N15,4) MAXIMO' MARK *SK-NUM(IND)
END-IF
COMPUTE WK-TOTAL = WK-TOTAL + (VAL(SK-NUM(IND)))
IF WK-TOTAL > 999999999999999,9999
   OR WK-TOTAL < -999999999999999,9999
   REINPUT FULL 'RESULTADO DEMASIADO GRANDE' MARK *SK-NUM(IND)
END-IF
END-SUBROUTINE
*
DEFINE RESTA
IF SK-NUM(IND) IS (N15,4)
   IGNORE
ELSE
  REINPUT FULL 'TECLEE NUMERO (N15,4) MAXIMO' MARK *SK-NUM(IND)
END-IF
COMPUTE WK-TOTAL = WK-TOTAL - (VAL(SK-NUM(IND)))
IF WK-TOTAL > 999999999999999,9999
   OR WK-TOTAL < -999999999999999,9999
   REINPUT FULL 'RESULTADO DEMASIADO GRANDE' MARK *SK-NUM(IND)
END-IF
END-SUBROUTINE
DEFINE MULTIPLICA
IF SK-NUM(IND) IS (N15,4)
   IGNORE
ELSE
  REINPUT FULL 'TECLEE NUMERO (N15,4) MAXIMO' MARK *SK-NUM(IND)
END-IF
COMPUTE WK-TOTAL = WK-TOTAL * (VAL(SK-NUM(IND)))
IF WK-TOTAL > 999999999999999,9999
   OR WK-TOTAL < -999999999999999,9999
   REINPUT FULL 'RESULTADO DEMASIADO GRANDE' MARK *SK-NUM(IND)
END-IF
END-SUBROUTINE
DEFINE DIVIDE
IF SK-NUM(IND) IS (N15,4)
   IGNORE
ELSE
  REINPUT FULL 'TECLEE NUMERO (N15,4) MAXIMO' MARK *SK-NUM(IND)
END-IF
COMPUTE WK-TOTAL = WK-TOTAL / (VAL(SK-NUM(IND)))
IF WK-TOTAL > 999999999999999,9999
   OR WK-TOTAL < -999999999999999,9999
   REINPUT FULL 'RESULTADO DEMASIADO GRANDE' MARK *SK-NUM(IND)
END-IF
END-SUBROUTINE
DEFINE ADIOS
 RESET SK-OPER(*)
 MOVE RIGHT '_____' TO SK-NUM(*)
 FOR IND = 15 TO  7 STEP -2
     SET CONTROL 'N'
     IF IND = 15 OR = 11 OR = 7
 MOVE LEFT '_/___' TO SK-NUM (IND - 5)
 MOVE LEFT '_\O__' TO SK-NUM (IND - 4)
 MOVE LEFT '__|\/' TO SK-NUM (IND - 3)
 MOVE LEFT '__|/\' TO SK-NUM (IND - 2)
 MOVE LEFT '_/___' TO SK-NUM (IND - 1)
 MOVE LEFT '_\___' TO SK-NUM (IND)
 END-IF
     IF IND = 13 OR =  9 OR =  5
 MOVE LEFT '____\' TO SK-NUM (IND - 5)
 MOVE LEFT '___O/' TO SK-NUM (IND - 4)
 MOVE LEFT '_\/|_' TO SK-NUM (IND - 3)
 MOVE LEFT '_/\|_' TO SK-NUM (IND - 2)
 MOVE LEFT '____\' TO SK-NUM (IND - 1)
 MOVE LEFT '____/' TO SK-NUM (IND)
 END-IF
     INPUT MAP 'MCALC'
     MOVE RIGHT '_____' TO SK-NUM(*)
END-FOR
END-SUBROUTINE
*
DEFINE CIAO
 RESET SK-OPER(*)
 MOVE RIGHT '_____' TO SK-NUM(*)
 FOR IND = 15 TO  5  STEP -2
     SET CONTROL 'N'
     IF IND = 15 OR = 11 OR = 7
 MOVE LEFT '__@__' TO SK-NUM (IND - 3)
 MOVE LEFT '(_|_)' TO SK-NUM (IND - 2)
 MOVE LEFT '|(|)|' TO SK-NUM (IND - 1)
 MOVE LEFT '((_))' TO SK-NUM (IND)
 END-IF
     IF IND = 13 OR =  9 OR =  5
 MOVE LEFT '"___"' TO SK-NUM (IND - 4)
 MOVE LEFT '(_@_)' TO SK-NUM (IND - 3)
 MOVE LEFT '_(|)_' TO SK-NUM (IND - 2)
 MOVE LEFT '_(|)_' TO SK-NUM (IND - 1)
 MOVE LEFT '(___)' TO SK-NUM (IND)
 END-IF
     INPUT MAP 'MCALC'
     MOVE RIGHT '_____' TO SK-NUM(*)
END-FOR
END-SUBROUTINE
END
