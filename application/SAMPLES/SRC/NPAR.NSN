************************************************************************
* NRHWSPES - PROGRAMA PARA ESCRIBIR UN VALOR EN TEXTO
* AUTOR    - CONSIST
* DATA     - 10/09/88
* MANUTENCOES: 23.04.93
* AUTOR        DATA      DESCRICAO
* SAUL
* CONSIST ARG  04/06/2016  EMBARGOS - MIGRACION ADA-SQL - MIGRADO DE HRPOLDES
*                                                         SIN MODIFICACIONES
************************************************************************
* PARAMETROS :
*  - #AREA(A140)    /* VALOR POR EXTENSO ...... OUTPUT
*      #VALOR(P15)  /* VALOR INFORMADO ........ INPUT
************************************************************************
DEFINE DATA PARAMETER
1 #PARM-VALOR        (N13,2) (ad=a)
* 1 #PARM-VALOR1       (N13,2) (ad=a)
1 #PARM-TEXTO        (A116) (ad=o)
*
LOCAL
1 #VALOR        (P13,2)
1 #AREA         (A116)
/*
01 #SAIDA (A90)
01 #EXT   (A20/3)
01 #CONECT (A1)
01 #VALOR-N(N13,2)
01 REDEFINE #VALOR-N
   02 #TRILHAO(N1)
   02 #BILHAO(N3)
   02 #MILHAO(N3)
   02 #MILHAR(N3)
   02 #UNIDADE(N3)
   02 #CENTAVO(N2)
01 #EXTENSO(A180)
01 REDEFINE #EXTENSO
   02 #EXTENSO-R(A60/3)
01 #GRUPO(N3)
01 REDEFINE #GRUPO
   02 #CEM(N1)
   02 #DEZ-UNI (N2)
   02 REDEFINE #DEZ-UNI
      03 #DEZ(N1)
      03 #UNI(N1)
01 #TAB-EXT-UNI(A06/09) CONST
   (V) <'UN','DOS','TRES','CUATRO','CINCO','SEIS','SIETE','OCHO',
         'NUEVE'>
01 #TAB-EXT-XXX(A19/09) CONST
   (V) <'ONCE','DOCE','TRECE','CATORCE','QUINCE','DIECISEIS',
        'DIECISIETE','DIECIOCHO','DIECINUEVE'>
01 #TAB-EXT-DEZ(A09/09) CONST
   (V) <'DIEZ','VEINTE','TREINTA','CUARENTA','CINCUENTA','SESENTA',
        'SETENTA','OCHENTA','NOVENTA'>
01 #TAB-EXT-CEM(A13/09) CONST
  (V) <'CIENTO','DOSCIENTOS','TRESCIENTOS','CUATROCIENTOS','QUINIENTOS',
        'SEISCIENTOS','SETECIENTOS','OCHOCIENTOS','NOVECIENTOS'>
END-DEFINE
************************************************************************ 
FORMAT LS=132
*
ASSIGN #VALOR = #PARM-VALOR 
RESET #AREA #PARM-TEXTO

   IF #VALOR LT 0 
              ESCAPE ROUTINE 
   END-IF 
* 
   MOVE #VALOR  TO #VALOR-N 
   MOVE 'PESOS' TO #AREA
*
   MOVE #TRILHAO TO #GRUPO
   IF #GRUPO NE 0
      PERFORM MONTA-EXTENSO
      IF #GRUPO > 1
         COMPRESS #SAIDA 'TRILLONES' INTO #AREA
      ELSE
         COMPRESS #SAIDA 'TRILLON' INTO #AREA
      END-IF
   END-IF
*****
   MOVE #BILHAO TO #GRUPO
   IF #GRUPO NE 0
      PERFORM MONTA-EXTENSO
      IF #GRUPO > 1
         COMPRESS #AREA #SAIDA 'BILLONES' INTO #AREA
      ELSE
         COMPRESS #AREA #SAIDA 'BILLON' INTO #AREA
      END-IF
   END-IF
*****
   MOVE #MILHAO TO #GRUPO
   IF #GRUPO NE 0
      PERFORM MONTA-EXTENSO
      IF #GRUPO > 1
         COMPRESS #AREA #SAIDA 'MILLONES' INTO #AREA
      ELSE
         COMPRESS #AREA #SAIDA 'MILLON' INTO #AREA
      END-IF
   END-IF
*****
   MOVE #MILHAR TO #GRUPO
   IF #GRUPO NE 0
      PERFORM MONTA-EXTENSO
      COMPRESS #AREA #SAIDA 'MIL' INTO #AREA
   END-IF
*****
   MOVE #UNIDADE TO #GRUPO
   IF #GRUPO NE 0
      PERFORM MONTA-EXTENSO
      COMPRESS #AREA #SAIDA INTO #AREA
***   IF #VALOR = 1
***      COMPRESS #AREA #SAIDA 'PESO' INTO #AREA
***   ELSE
***      COMPRESS #AREA #SAIDA 'PESOS' INTO #AREA
***   END-IF
** ELSE
***   IF #VALOR > 0 AND #MILHAR = 0
***      COMPRESS #AREA  'DE PESOS' INTO #AREA
***   ELSE
***      COMPRESS #AREA  'PESOS' INTO #AREA
***   END-IF
   END-IF
*****
   MOVE #CENTAVO TO #GRUPO
   IF #GRUPO NE 0
      PERFORM MONTA-EXTENSO
      IF #GRUPO > 1
       COMPRESS #AREA 'CON' #SAIDA 'CENTAVOS' INTO #AREA
      ELSE
       COMPRESS #AREA 'CON' #SAIDA 'CENTAVO' INTO #AREA
      END-IF
   END-IF
*****
MOVE ALL '_' TO #SAIDA 
COMPRESS #AREA #SAIDA INTO #AREA
/*
ASSIGN #PARM-TEXTO = #AREA 
*
*******************************
DEFINE SUBROUTINE MONTA-EXTENSO
*******************************
RESET #EXT(*) #CONECT #SAIDA
IF #GRUPO = 100
   MOVE 'CIEN' TO #EXT(1)
ELSE
   IF #CEM > 0
      MOVE #TAB-EXT-CEM (#CEM) TO #EXT(1)
   END-IF
END-IF
*
IF #DEZ-UNI = 11 THRU 19
   MOVE #TAB-EXT-XXX(#UNI) TO #EXT(3)
ELSE
   IF #DEZ NE 0
      MOVE #TAB-EXT-DEZ(#DEZ) TO #EXT(2)
      IF #UNI NE 0
          MOVE 'Y' TO #CONECT
          MOVE #TAB-EXT-UNI(#UNI) TO #EXT(3)
      END-IF
   ELSE
      IF #UNI NE 0
          MOVE #TAB-EXT-UNI(#UNI) TO #EXT(3)
      END-IF
   END-IF
END-IF
*
COMPRESS #EXT(1) #EXT(2) #CONECT #EXT(3) INTO #SAIDA
END-SUBROUTINE
*******
END
