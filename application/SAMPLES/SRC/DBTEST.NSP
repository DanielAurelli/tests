********************************************************************************
* PROGRAM DBTEST 
* (SEND SQL COMMANDS TO DATABASE)
********************************************************************************
*
DEFINE DATA
LOCAL
1 CAT VIEW OF CATEGORIES
  2 CATEGORY_ID 
  2 CATEGORY_NAME
  2 DESCRIPTION
**
1 #DB_TP (I4)
1 #TABLENAME(A32)
1 #OWNERNAME(A30)
1 #COUNT (I4)
1 #OPTION(N1)
END-DEFINE
*
FORMAT LS=133
SET KEY PF3 NAMED 'STOP'
LIMIT 60
*
MOVE 'NFWERRTA' TO *ERROR-TA
*
IF *MACHINE-CLASS = 'MAINFRAME' THEN
  ASSIGN #DB_TP := 7
ELSE
  CALL 'DBTYPE' #DB_TP
END-IF
REPEAT
  INPUT 10x 'DATABASE VALIDATION TESTS'
  //
    /
     'ENTER OPTION>' #OPTION (AD=AI SG=OFF ZP=ON)
    / '-'(40) 
    / 5x '1 - CREATE TABLE' 
    / 5x '2 - INSERT SOME RECORDS INTO TABLE' 
    / 5x '3 - SELECT AND LIST RECORDS' 
    / 5x '4 - UPDATE RECORDS' 
    / 5x '5 - DELETE RECORDS' 
    / 5x '6 - DROP TABLE' 
    / 5x '0 - EXIT' 
*
IF *PF-KEY='PF3' OR #OPTION = 0 ESCAPE BOTTOM END-IF
DECIDE ON FIRST VALUE OF #DB_TP
  VALUE 0   /* ORACLE
    PERFORM EXEC_ORACLE
  VALUE 7   /* DB2
    PERFORM EXEC_DB2
  VALUE 10  /* ADABAS-D
    PERFORM  EXEC_ADABASD
  VALUE 15  /* POSTGRESQL
    PERFORM EXEC_POSTGRESQL
  VALUE 12  /* SQL SERVER
    PERFORM EXEC_SQLSERVER
  NONE VALUE
    ESCAPE ROUTINE
END-DECIDE
END-REPEAT
WRITE 'END OF PROGRAM'
ESCAPE ROUTINE
*
********************************************************************************
DEFINE SUBROUTINE EXEC_ORACLE
*
PERFORM EXEC_DB2 /* EQUIVALENT
*
*
*
END-SUBROUTINE
********************************************************************************
DEFINE SUBROUTINE EXEC_DB2
*
IF #OPTION = 1  
  PROCESS SQL CATEGORIES <<
    CREATE TABLE CATEGORIES ( 
    CATEGORY_ID INTEGER NOT NULL, 
    CATEGORY_NAME CHAR(16),
    DESCRIPTION VARCHAR(250) ,
    CONSTRAINT CATEGORIES_PKEY PRIMARY KEY (CATEGORY_ID) )  
  >>
END-IF
  
IF #OPTION = 2  
 SELECT COUNT(*) INTO #COUNT FROM CATEGORIES
   WRITE 'COUNT:' #COUNT  
 END-SELECT
 
 INSERT INTO CATEGORIES 
 (CATEGORY_ID, CATEGORY_NAME, DESCRIPTION)
 VALUES ( 1 , 'BEVERAGES' , 'JUICE') 
 WRITE "INSERT VALUES ( 1 , 'BEVERAGES' , 'JUICE')" 
 INSERT INTO CATEGORIES 
 (CATEGORY_ID, CATEGORY_NAME, DESCRIPTION)
 VALUES ( 2 , 'HARDWARE' , 'HAMMERS') 
 WRITE "INSERT VALUES ( 2 , 'HARDWARE' , 'HAMMERS')" 
 INSERT INTO CATEGORIES 
 (CATEGORY_ID, CATEGORY_NAME, DESCRIPTION)
 VALUES ( 3 , 'BEVERAGES' , 'SOFT DRINKS') 
 WRITE "INSERT VALUES ( 3 , 'BEVERAGES' , 'SOFT DRINK')" 
 INSERT INTO CATEGORIES 
 (CATEGORY_ID, CATEGORY_NAME, DESCRIPTION)
 VALUES ( 4 , 'BEVERAGES' , 'WATER') 
 WRITE "INSERT VALUES ( 4 , 'BEVERAGES' , 'WATER')" 
 COMMIT
 SELECT COUNT(*) INTO #COUNT FROM CATEGORIES
   WRITE 'COUNT:' #COUNT  
 END-SELECT
END-IF

IF #OPTION = 3  
  
 S1. SELECT * INTO VIEW CAT FROM CATEGORIES
   DISPLAY CATEGORY_ID CATEGORY_NAME(AL=20) DESCRIPTION (AL=40) 
 END-SELECT
 SELECT COUNT(*) INTO #COUNT FROM CATEGORIES
   WRITE 'COUNT RECORDS:' #COUNT  
 END-SELECT

END-IF

IF #OPTION = 4  
 S2. SELECT * INTO VIEW CAT FROM CATEGORIES
   WHERE CATEGORY_NAME='BEVERAGES' 
   MOVE 'JUICES, SOFT DRINKS AND WATER' TO DESCRIPTION
   WRITE 'UPDATE' CATEGORY_ID DESCRIPTION (AL=40) 
   UPDATE(S2.)
 END-SELECT
 COMMIT
 
END-IF

IF #OPTION = 5  
 DELETE FROM CATEGORIES 
   WHERE CATEGORY_ID BETWEEN 1 AND 4 
 WRITE 'DELETE' 
 COMMIT  

END-IF

IF #OPTION = 6  
 PROCESS SQL CATEGORIES <<
      DROP TABLE CATEGORIES
  >>
END-IF
WRITE 'OK'
*
*
END-SUBROUTINE
********************************************************************************
DEFINE SUBROUTINE EXEC_ADABASD
*
  ESCAPE ROUTINE
*
END-SUBROUTINE
********************************************************************************
DEFINE SUBROUTINE EXEC_POSTGRESQL
*
  ESCAPE ROUTINE
*
END-SUBROUTINE
********************************************************************************
DEFINE SUBROUTINE EXEC_SQLSERVER
*
  ESCAPE ROUTINE
*
END-SUBROUTINE
********************************************************************************
END


